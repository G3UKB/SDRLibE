(*
	pubsub.8th
 
  Publish/Subscribe interface
 
 Copyright C 2018 by G3UKB Bob Cowdery
 This program is free software; you can redistribute it and/or modify
 it under the terms of the GNU General Public License as published by
 the Free Software Foundation; either version 2 of the License, or
 at your option any later version.
 
  This program is distributed in the hope that it will be useful,
  but WITHOUT ANY WARRANTY; without even the implied warranty of
  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
  GNU General Public License for more details.
 
  You should have received a copy of the GNU General Public License
  along with this program; if not, write to the Free Software
  Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
 
  The author can be reached by email at:   
     bob@bobcowdery.plus.com
*)

\ ========================================
\ Testing only

needs debug/sed
\ true SED-CHECK

\ Set namespace
ns: C

\ =============================================================================================
\ Publish Subscribe task
\ ========================================
\ General Helpers
\
\ Return the data element
: data@	SED: m -- *
	\ in: 	{ "type" : ... , "data" : Any }
	\ out:	Opaque data
	"data" m:@ nip ;

\ Return the pub/sub store	
: store@	SED: -- m
	\ in: 	none
	\ out:	pub/sub store
	"ps_s" t:@ ;
\ Retrieve the event name
: evnt_name@ SED: m -- s
	\ in: 	{ "type" : ... , "data" : [evnt-name, f] }
	\ out:	evnt-name
	data@ 0 a:@ nip ;
	
\ ========================================
\ Registration helpers
\
\ Retrieve the event flag
: evnt_flg@ SED: m -- T
	\ in: 	{ "type" : ... , "data" : [evnt-name, f] }
	\ out:	flag
	data@ 1 a:@ nip ;	

\ Does the event name already exist
: ps_exists?	SED: m -- F
	\ in: 	{ "type" : ... , "data" : [evnt-name, f] }
	\ out:	flag
	evnt_name@ store@ swap m:exists? nip ;

\ Return default event data
: ps_make_evnt_data	SED: m -- a
	\ in: 	{ "type" : ... , "data" : [evnt-name, f] }
	\ out:	[evnt_flg, []]
	a:new 0 rot evnt_flg@ a:! 1 a:new a:! ; 

\ Store initial event data
: ps_store_new	SED: a m -- 
	\ in: 	{ "type" : ... , "data" : [evnt-name, f] }
	\				[f, []]
	\ out:	[evnt_flg, []]
	evnt_name@ store@ swap rot m:! drop ;

\ Register a new entry	
: ps_reg_new	SED: m --
	\ in: 	{ "type" : ... , "data" : [evnt-name, f] }
	\ out:	none
	dup ps_make_evnt_data
	swap ps_store_new ;

\ ========================================
\ Subscribe helpers
\
\ Retrieve the callback word
: evnt_callback@ SED: m -- w
	\ in: 	{ "type" : ... , "data" : [w] }
	\ out:	w
	data@ 0 a:@ nip ;
	
\ ========================================
\ Message handlers
\ Initialise the p/s structure
: do_init SED: m --
	\ in: 	{ "type" : "init" }
	\ out:	none
	\
	\ Create the dictionary in a task var
	\ Form is as follows
	\ {"name": [f,[w1,w2,w3...]], "name1": [f,[w4,w5,w6...]], ...}
	drop {} "ps_s" t:! ;

\ Register a new event
: do_reg	SED: m --
	\ in: 	{ "type" : "reg" , "data" : [evnt-name, f] }
	\ out:	none
	\ data: [evnt-name, f] where f==true if dispatch on main thread
	dup >r
	ps_exists? if
		"The registration " r> evnt_name@ s:+ " already exists!" s:+ throw  
	else
		\ Create a new registration
		r> ps_reg_new
	then ;

\ Subscribe to an event
: do_sub	SED: m --
	\ in: 	{ "type" : "sub" , "data" : [event-name, w]}
	\ out:	none
	dup >r
	ps_exists? if
		\ Add a new subscriber to the event
		\ Get the callback array
		store@ r@ evnt_name@ m:@ nip
		\ Get the callback word array [f,[w,w, ..]]
		1 a:@
		\ Store the new callback word in the array
		r@ evnt_callback@ a:push
	else
		"No registration " r> evnt_name@ s:+ " to subscribe to" s:+ throw 
	then ;
		
\ Publish an event	
: do_pub	SED: m --
	\ in: 	{ "type" : pub , "data" : Opaque }
	\ out:	none
	"Pub:" . . cr ;

\ Terminate task
: do_term SED: m --
	\ in: 	{ "type" : "term" }
	\ out:	none
	drop false "pub_sub_run" t:!
;

\ Decode and dispatch to message handlers
: msg_decode	SED: m -- 
	\ in: 	{"type" : "init"|"reg"|"sub"|"pub|"term", "data" : Any}
	\ out:	none
	"type" m:@
	{ "init" : ' do_init , "reg" : ' do_reg , "sub" : ' do_sub , "pub" : ' do_pub , "term" : ' do_term }
	case
;

\ =============================================================================================
\ Task entry point
\ All events are handled by this task which dispatches:
\ 	- on its thread for SYS events
\		- on the main thread for UI events
: pub_sub_task	 SED: --
	\ in: 	none
	\ out:	none
	\ Give us a name
	"PUB-SUB-TASK" t:name!
	\ Set us in the run state
	true "pub_sub_run" t:!
	
	\ Wait on the task q for work
	\ Up the q a little, should never really get that high
	20 t:def-queue
	\ Set q to return null on empty
	t:getq false q:throwing drop
	
	\ Loop while running
	repeat
		\ Wait for a wake up call or timeout
		100 t:q-wait
		\ Empty the q
		repeat
			t:pop null? if
				drop false
			else
				\ Message to process
				msg_decode true
			then
		while!
		\ Check for exit
		"pub_sub_run" t:@
	while!
	"pub_sub_task - exiting..." log
;

\ Reset namespace
ns: user

\ =============================================================================================
\ Main code can be called from any thread
null var, t_pubsub

\ Initialise publish/subscribe system
: pubsub_init	SED: --
	' C:pub_sub_task t:task dup t_pubsub !
	{"type" : "init"} t:push ;

\ Terminate publish/subscribe system
: pubsub_term	SED: --
	t_pubsub @ {"type" : "term"} t:push ;
	
\ Register a new event
\ All events must be registered before they can be used
\ Event names are just strings
: pubsub_register	 SED: T s -- 
	\ in:	s - event name 	
	\			f - true if to be dispatched on main thread else p/s thread
	\ out:	none
	a:new 0 rot a:! 1 rot a:!
	{"type" : "reg"} "data" rot m:!
	t_pubsub @ swap t:push ;

\ Subscribe to an new
: pubsub_subscribe	 SED: s w -- 
	\ in:	s - event name	
	\			w - callback word 	
	\ out:	none
	a:new 1 rot a:! 0 rot a:!
	{"type" : "sub"} "data" rot m:!
	t_pubsub @ swap .s t:push ;


\ =============================================================================================
\ Testing

: callback_wd
	"Hi, callback received" log ;
	
: testit
	pubsub_init
	
	\ Calls fail
	true "Event1" pubsub_register
	0.2 sleep
	true "Event2" pubsub_register
	0.2 sleep
	"Event1" ' callback_wd pubsub_subscribe
	
	(*
	\ In-line works
	true "Event1" a:new 0 rot a:! 1 rot a:!
	{"type" : "reg"} "data" rot m:!
	t_pubsub @ swap .s t:push
	
	true "Event2" a:new 0 rot a:! 1 rot a:!
	{"type" : "reg"} "data" rot m:!
	t_pubsub @ swap .s t:push
	*)
	
	pubsub_term
;

testit
1 sleep
bye
	