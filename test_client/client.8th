(* 
	client
 
  Script client to SDRLibE SDR library
 
 Copyright C 2018 by G3UKB Bob Cowdery
 This program is free software; you can redistribute it and/or modify
 it under the terms of the GNU General Public License as published by
 the Free Software Foundation; either version 2 of the License, or
 at your option any later version.
 
  This program is distributed in the hope that it will be useful,
  but WITHOUT ANY WARRANTY; without even the implied warranty of
  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
  GNU General Public License for more details.
 
  You should have received a copy of the GNU General Public License
  along with this program; if not, write to the Free Software
  Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA
 
  The author can be reached by email at:   
     bob@bobcowdery.plus.com
*)

needs debug/sed
true SED-CHECK

\ Set namespace
ns: sdr-script

\ ======================================================================================
\ Initialisation
\ Create UDP socket
{ "domain" : ` net:INET4 ` , "type" : ` net:DGRAM ` , "proto" : ` net:PROTO_UDP ` } net:socket var, udp_cmd_sock
\ Accessor
: cmd_sock	\ -- sock
	udp_cmd_sock @ ;

\ ======================================================================================
\ Buffers
5000 b:new 0x0 b:fill var, b_resp_buffer
: resp_buffer	SED: -- b
 b_resp_buffer @ ;
 
\ ======================================================================================
\ Messages
: discover_msg 			SED: -- s
	{ "cmd" : "radio_discover" , "params" : [] } >json ;
	
: server_start_msg 	SED: -- s
	{ "cmd" : "server_start" , "params" : [] } >json ;
	
: radio_start_msg 	SED: n -- s
	>r
	{ "cmd" : "radio_start" , "params" : [] } 
	"params" m:@ r> a:push drop >json ;
	
: enum_outputs_msg 	SED: -- s
	{ "cmd" : "enum_outputs" , "params" : [] } >json ;
	
: default_route_msg	SED: s s -- s
	>r >r
	{ "cmd" : "set_audio_route" , "params" : [1, "LOCAL", 1, "", "", "BOTH"] }
	"params" m:@ 3 r> a:! 4 r> a:! drop >json ;
	
\ ======================================================================================
\ Net
\ ==================
\ Address management
: cmd_port	SED: -- n
	10010 ;
	
: data_1_port	SED: -- n
	10011 ;
	
: data_2_port	SED: -- n
	10012 ;
	
: data_3_port	SED: -- n
	10013 ;
	
: cmd_addr		SED: -- X
	"127.0.0.1" cmd_port net:getaddrinfo ;
	
: data_1_addr	SED: -- X
	"127.0.0.1" data_1_port net:getaddrinfo ;
	
: data_2_addr	SED: -- X
	"127.0.0.1" data_2_port net:getaddrinfo ;
	
: data_3_addr	SED: -- X
	"127.0.0.1" data_3_port net:getaddrinfo ;

\ ==================
\ Message send/receive

\ Send given command
: send_cmd_msg	SED: s -- 
	s:len >r
	cmd_sock cmd_addr rot 0 net:sendto nip
	r> n:= not if "Short send!" log then ;

\ Net wait
: net_wait	SED:  -- G
	cmd_sock true 2000 net:wait ;

: recv_from	SED:  -- G
	resp_buffer 0 net:recvfrom null? if 
		"Error in read!" log drop false
	else
		drop nip nip nip json> true 
	then ;
		
\ Wait for response
: wait_resp_with_timeout	SED:  -- b G
	net_wait if
		recv_from
	else
		"Timeout waiting for response!" log false
	then ;
	
	\ ======================================================================================
	\ Protocol words
	
	: get_default_route	SED:  -- s s
		enum_outputs_msg send_cmd_msg
		wait_resp_with_timeout not if 
			"Error getting audio info!" . cr 
		else
			"outputs" m:@ nip 1 a:@ nip
			"api" m:@ swap "name" m:@ nip 
		then ;
	
	\ ======================================================================================
	\ Script words
	
	: basic_script	SED:  --
		discover_msg send_cmd_msg
		wait_resp_with_timeout not if ;; then "Discover: " . . cr
		get_default_route default_route_msg send_cmd_msg
		wait_resp_with_timeout not if ;; then  "Route: " . . cr
		server_start_msg send_cmd_msg
		wait_resp_with_timeout not if ;; then  "Server start: " . . cr
		0 radio_start_msg send_cmd_msg
		wait_resp_with_timeout not if ;; then  "Radio start: " . . cr ;
		
	\ ======================================================================================
	\ Execution
	
	basic_script
	5 sleep
	bye